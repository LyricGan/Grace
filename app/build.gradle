apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.lyricgan.grace"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        flavorDimensions "default"

        // dex突破65535的限制
        multiDexEnabled true

        manifestPlaceholders = [
                CHANNEL_VALUE: "common"
        ]

        ndk {
            // 设置.so文件支持的CPU架构
            abiFilters 'armeabi-v7a'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            File propFile = file('signing.properties')
            if (propFile.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(propFile))
                if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                        props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
                    android.signingConfigs.debug.storeFile = file(props['STORE_FILE'])
                    android.signingConfigs.debug.storePassword = props['STORE_PASSWORD']
                    android.signingConfigs.debug.keyAlias = props['KEY_ALIAS']
                    android.signingConfigs.debug.keyPassword = props['KEY_PASSWORD']
                } else {
                    android.buildTypes.debug.signingConfig = null
                }
            } else {
                android.buildTypes.debug.signingConfig = null
            }
        }
        release {
            File propFile = file('signing.properties')
            if (propFile.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(propFile))
                if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                        props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
                    android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
                    android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
                    android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
                    android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
                } else {
                    android.buildTypes.release.signingConfig = null
                }
            } else {
                android.buildTypes.release.signingConfig = null
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true

            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            if (variant.buildType.name == 'release') {
                variant.outputs.all {
                    outputFileName = "Grace_${variant.productFlavors[0].name}_${defaultConfig.versionName}_${releaseTime()}.apk"
                }
            }
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    // 渠道配置
    productFlavors {
        common {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "4g"
    }
}

def static releaseTime() {
    return new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("GMT+08"))
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.13.2'

    implementation project(':library_base')
    implementation project(':library_util')
    implementation project(':library_widget')
    implementation project(':library_json')
    implementation project(':library_imageloader')
    implementation project(':library_network')
    implementation project(':library_eventbus')
    implementation project(':module_kotlin')

    implementation 'com.zhihu.android:matisse:0.5.3-beta3'

    // 下拉刷新，上拉加载组件，https://github.com/scwang90/SmartRefreshLayout
    implementation  'com.scwang.smart:refresh-layout-kernel:2.0.3'      // 核心必须依赖
    implementation  'com.scwang.smart:refresh-header-classics:2.0.3'    // 经典刷新头
    implementation  'com.scwang.smart:refresh-header-radar:2.0.3'       // 雷达刷新头
    implementation  'com.scwang.smart:refresh-header-falsify:2.0.3'     // 虚拟刷新头
    implementation  'com.scwang.smart:refresh-header-material:2.0.3'    // 谷歌刷新头
    implementation  'com.scwang.smart:refresh-header-two-level:2.0.3'   // 二级刷新头
    implementation  'com.scwang.smart:refresh-footer-ball:2.0.3'        // 球脉冲加载
    implementation  'com.scwang.smart:refresh-footer-classics:2.0.3'    // 经典加载
}

